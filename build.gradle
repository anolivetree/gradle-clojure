plugins {
  id 'org.ajoberstar.grgit' version '2.0.0'
//  id 'org.ajoberstar.reckon' version '0.2.1'
  id 'com.diffplug.gradle.spotless' version '3.6.0'

  id 'gradle-clojure.clojure' version '0.3.0-rc.3' apply false
  id 'org.ajoberstar.stutter' version '0.2.0' apply false
  id 'com.gradle.plugin-publish' version '0.9.9' apply false
}

/*
reckon {
  normal = scopeFromProp()
  preRelease = stageFromProp('milestone', 'rc', 'final')
}
*/

allprojects {
  apply plugin: 'com.diffplug.gradle.spotless'

  group = 'io.github.gradle-clojure'

  repositories {
    jcenter()
    google()
    maven { url = 'https://clojars.org/repo' }
  }

  plugins.withId('maven-publish') {
    publishing {
      repositories {
        maven {
          name = 'bintray'
          url = 'https://api.bintray.com/maven/gradle-clojure/maven/gradle-clojure/;publish=1'
          credentials {
            username = System.env['BINTRAY_USER']
            password = System.env['BINTRAY_KEY']
          }
        }
      }
    }
  }

  plugins.withId('java') {
    sourceCompatibility = '1.8'

    task sourcesJar(type: Jar) {
      from sourceSets.main.allSource
      classifier = 'sources'
    }

    // ensure consistent formatting
    spotless {
      java {
        eclipse().configFile rootProject.file('gradle/eclipse-java-google-style-nowrap.xml')
        licenseHeaderFile file('gradle/HEADER')
      }
      format('gradle') {
        target '**/*.gradle'
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
      }
    }
  }
}
